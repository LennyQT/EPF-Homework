to create todo_name
	Log "Creating todo [" & todo_name & "]"
	
	Click "Todos/Add_todo_field"
	TypeText todo_name, Return
	
	WaitFor 8.0, (Text:todo_name, TextDifference:1, SearchRectangle:( "Todos/Todo_list_TopLeft", "Todos/Todo_list_BottomRight"))
	
	LogSuccess "Todo [" & todo_name & "] created"
end create


// Returns the found <todo_name> as text, if not returns false
function find todo_name
	Log "Searching for todo [" & todo_name & "]"
	
	// Loop through the unticked checkboxes to find our todo
	repeat with each item of EveryImageLocation("Todos/Unticked_checkbox")
		
		// Create a large rectangle to the right of our checkbox
		put (it.x + 6, it.y - 8) into TL
		put (it.x + 300, it.y + 8) into BR
		
		put ReadText(TL, BR) into todo_text
		Log "TEXT: " & todo_text
		
		// return our found todo as text
		if todo_text equals todo_name			
			LogSuccess "Todo [" & todo_name & "] found"
			return todo_text
			exit function
		end if
		
		put "" into todo_text
	end repeat
	
	return false
end find


// Opens the detailed view of <todo_name>
to openDetailView todo_name	
	get Todo.find(todo_name)
	if it does not equal false		
		DoubleClick (TEXT: it)
		WaitFor 8.0, "Todos/DetailView_TL"
	end if		
end openDetailView

// Closes the current detailed view
to closeDetailView
	Click "Todos/Close_detail_view"
	WaitFor 8.0, ("Todos/Todo_list_BottomRight")
end closeDetailView


// Double clicks a <todo> and adds a <substask> to it
to addSubtask todo_name, subtask_name
	Log "Adding subtask [" & subtask_name & "] to todo [" & todo_name & "]"
	
	// Check that the detailed view has been opened
	if ImageFound(1.0, "Todos/DetailView_TL") is false
		LogError "Detail view not opened"
		openDetailView todo_name
	end if
	
	if findSubtask(subtask_name) is false		
		Click "Todos/AddSubTask_box", 
		TypeText subtask_name, return
		WaitFor 8.0, (TEXT: subtask_name, SearchRectangle:("Todos/DetailView_TL", "Todos/DetailView_BR"))
		
		LogSuccess "Subtask  [" & subtask_name & "] added"
	end if	
end addSubtask


// returns true if subtask exists
function findSubtask subtask_name
	Log "Searching for todo [" & subtask_name & "]"
	
	// Loop through the unticked checkboxes to find our todo
	repeat with each item of EveryImageLocation("Todos/Unticked_taskbox")
		
		// Create a large rectangle to the right of our checkbox
		put (it.x + 6, it.y - 8) into TL
		put (it.x + 200, it.y + 8) into BR
		
		put ReadText(TL, BR) into subtask_text
		
		// return our found todo as text
		if subtask_text equals subtask_name			
			LogSuccess "Subtask [" & subtask_name & "] found"
			return true
			exit function
		end if
	end repeat
	
	return false
end findSubtask

// adds a due date to the todo task
to addDueDate todo_name, due_date
	Log "Adding due date: [" & due_date & "]"
	
	// put our date into a proper date format
	put the date(due_date) into due_date
	
	if ImageFound(1.0, "Todos/DetailView_TL") is false
		LogError "Detail view not open"
		openDetailView todo_name
	end if
	
	Click "Todos/Date/DueDate_Icon"
	WaitFor 8.0, "Todos/Date/DayRect_BR"
	
	// Loop until the month and year matches our date on the calendar
	repeat while displayed_date is not equal to due_date	
		put ReadText("Todos/Date/DateView_TL","Todos/Date/DateView_BR") into date_text
		put the date((due_date.day & " " & date_text)) into displayed_date
		
		if displayed_date < due_date
			Click "Todos/Date/IncreaseDate_button"
		end if
		
		if displayed_date > due_date
			Click "Todos/Date/DecreaseDate_button"
		end if
		
		wait 2
	end repeat
	
	Click (TEXT: due_date.day, ContrastColor:"AutoDetect", Contrast:"On", SearchRectangle:("Todos/Date/DayRect_TL", "Todos/Date/DayRect_BR"))
	Click "Todos/Date/DateSave_button"
	WaitFor 5.0, "Todos/Date/DueDate_Icon/DueDate_Icon_Highlighted"
	
	LogSuccess "Due date added"
end addDueDate